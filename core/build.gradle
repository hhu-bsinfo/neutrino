plugins {
    id 'idea'
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id "com.jfrog.artifactory" version "4.13.0"

    id "io.github.krakowski.jextract" version "0.1.0"
}

compileJava {
    sourceCompatibility = '16'
    targetCompatibility = '16'
    options.encoding = 'UTF-8'
}

group = 'de.hhu.bsinfo'
version = getProperty('projectVersion')

jextract {
    library = 'ibverbs'
    targetPackage = 'org.linux.rdma'
    header = "${project.projectDir}/src/main/c/ibverbs.h"
}

compileJava {
    dependsOn(':native:build')
    options.compilerArgs += ['-h', project(':native').file('src/main/public')]
}

compileJava.doLast {
    project.copy {
        from file("${project(':native').buildDir}/lib/main/debug/libneutrino.so")
        into file("${project.projectDir}/src/main/resources/NATIVE/x86_64")
    }
}

repositories {
    mavenCentral()
    jcenter()

    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

jar {
    from fileTree('build/generated/sources/jextract/main/java') {
        include '**/*.class'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    api 'org.agrona:agrona:1.3.0'
    api 'org.slf4j:slf4j-api:1.7.26'
    implementation 'org.jetbrains:annotations:17.0.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.1'
    testImplementation 'org.assertj:assertj-core:3.11.1'
    testImplementation 'org.slf4j:slf4j-nop:1.7.28'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.12'
    testCompileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
}

test {
    useJUnitPlatform()
}

apply from: 'publish.gradle'
apply from: 'bintray.gradle'
apply from: 'artifactory.gradle'